apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 14
        versionCode 90
        versionName "9.0"
    }

    // 使用 JDK 1.8
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    // noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:3.12.12'
    // noinspection GradleDependency
    implementation 'androidx.lifecycle:lifecycle-common:2.1.0'
    // noinspection GradleDependency
    implementation 'androidx.lifecycle:lifecycle-runtime:2.1.0'
}

publish {
    // 填写 bintray 用户名，注意大小写
    userOrg = 'getactivity'
    // 定义的 maven group id 最终引用形式
    groupId = 'com.hjq'
    // maven的 artifact id
    artifactId = 'http'
    // maven 上发布版本号
    publishVersion = '9.0'
    // 描述，自己定义
    desc = 'Easy-to-use network request framework'
    // 项目在 github 中的地址
    website = "https://github.com/getActivity/EasyHttp"
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}